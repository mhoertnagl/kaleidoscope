// http://lalrpop.github.io/lalrpop/tutorial/005_building_asts.html
use std::str::FromStr;
use crate::ast::{
  Module, Statement, Expr, Atom, Opcode
};

grammar;

// #[inline]
// Comma<T>: Vec<T> = {
//   <v:(<T> ",")*> <e:T?> => match e {
//       None => v,
//       Some(e) => {
//           let mut v = v;
//           v.push(e);
//           v
//       }
//   }
// };

#[inline]
Comma<T>: Vec<T> = {
    <i1: T> <i2:("," T)*> => {
        let mut items = vec![i1];
        items.extend(i2.into_iter().map(|e| e.1));
        items
    }
};

pub Module: Module = {
  <ss:Statement*> => Module {
    statements: ss,
  },
};

Statement: Statement = {
  "let" <n:Id> "=" <e:Expr> ";" => Statement::Let {
    name: n,
    expr: e,
  },
  "def" <n:Id> "(" <ps:Comma<Id>?> ")" "begin" <ss:Statement*> "end" => Statement::Def {
    name: n,
    params: match ps {
      None => vec![],
      Some(x) => x,
    },
    statements: ss,
  },
  <n:Id> "=" <e:Expr> ";" => Statement::Assign {
    name: n,
    expr: e,
  },
  <e:Expr> ";" => Statement::Expr {
    expr: e,
  },
  // "print" <e:Expr> => Statement::Expr {
  //   expr: e,
  // },
  "return" <e:Expr> ";" => Statement::Return {
    expr: e,
  },
}

Expr: Box<Expr> = {
  <l:Expr> <x:ExprOp> <r:Factor> => Box::new(Expr::BinOp {
    left: l,
    op: x,
    right: r,
  }),
  Factor,
};

ExprOp: Opcode = {
  "+" => Opcode::Add,
  "-" => Opcode::Sub,
};

Factor: Box<Expr> = {
  <l:Factor> <x:FactorOp> <r:Term> => Box::new(Expr::BinOp {
    left: l,
    op: x,
    right: r,
  }),
  Term,
};

FactorOp: Opcode = {
  "*" => Opcode::Mul,
  "/" => Opcode::Div,
};

Term: Box<Expr> = {
  <a:Atom> => Box::new(Expr::Atom(<>)),
  FunCall => <>,
  "(" <e:Expr> ")" => e,
};

FunCall: Box<Expr> = {
  <n:Id> "(" <args:Comma<Expr>?> ")" => Box::new(Expr::FunCall {
    name: n,
    args: match args {
      None => vec![],
      Some(x) => x,
    },
  })
}

Atom: Atom = {
  Num => Atom::Num(<>),
  Id => Atom::Id(<>),
};

Id: String = {
  r"[a-zA-Z][a-zA-Z0-9]*" => String::from(<>),
};

Num: f64 = {
  r"-?[0-9]+" => f64::from_str(<>).unwrap(),
  r"-?[0-9]*\.[0-9]*" => f64::from_str(<>).unwrap()
};
